begin model
#---
# About: 
#  - This model is intended to be consistent with the compartmental model
#    of Ye C, Lin YT et al. (2021), which describes COVID-19 transmission dynamics.
#  - The model is specified in terms of building blocks labeled
#    S, E, A, I, V, H, D, and R.
#     - The building blocks represent distinct populations.
#     - Some of the populations have population features, which have
#       multiple possible values.
#---
#---
# Reference:
#  - Ye C, Lin YT et al. (2021) medRxiv
#    https://www.medrxiv.org/content/10.1101/2021.10.19.21265223v1
#---
begin parameters
#######################################
# fixed population-secific parameters #
#######################################
# total population
# region-specific and fixed
S0 6280487
#############################################
# adjustable population-specific parameters #
#############################################
# NB: parameter names are not the same as in the reference.
# start time of the local epidemic 
#  Consider choosing ts0 to correspond to the first day for which
#  the cumulative number of detected cases was 200 or more.
#  NB: sigma -> ts0. ts0 is called sigma in the reference.
ts0 ts0__FREE
# start time of 1st social distancing period
ts1 ts1__FREE
# start times of social distancing periods *after* the initial
#  social-distancing period ts1
ts2 ts2__FREE 
ts3 ts3__FREE
ts4 ts4__FREE
ts5 ts5__FREE
#theta0, theta1: takeover times of Alpha and Delta, respectively
theta0 theta0__FREE
theta1 theta1__FREE
#y0, y1: transmissibility factors of Alpha and Delta, respectively 
y0 y0__FREE
y1 y1__FREE
# eigenvalues (sums of rate constants) that determine the time scales
#  for establishment of the stationary states of the various social distancing periods
lambda0 lambda0__FREE
lambda1 lambda1__FREE
lambda2 lambda2__FREE
lambda3 lambda3__FREE   
lambda4 lambda4__FREE
# stationary social distancing setpoints of the various social distancing periods
# The setpoint during a given period is the fraction of the regional population 
# practicing social distancing.
p0 p0__FREE
p1 p1__FREE#
p2 p2__FREE
p3 p3__FREE
p4 p4__FREE
# rate constant for disease transmission 
beta beta__FREE
# fraction of new symptomatic infections detected 
fD fD__FREE
# dispersion parameter of negative binomial dist characterizing noise in case detection
r r__FREE
##############################
# fixed universal parameters #
##############################
# initial number of infectious persons
I0 1 # dimensionless
# reporting period
#  (delta_t=1 => daily case reports)
delta_t 1 # d
# parameter characterizing protective effect of social distancing
mb 0.1 # dimensionless
# relative infectiousness of E vs. I
rhoE 1.1 # dimensionless
# relative infectiousness of A vs. I
rhoA 0.9 # dimensionless
# rate constant for progression through stages of the incubation period
kL 0.94 # /d
# rate constant for I->H or R transition
cI 0.12 # /d
# fraction of exposed individuals who never develop symptoms
fA 0.44 # dimensionless
# fraction of symptomatic cases that progress to severe disease
fH 0.054 # dimensionless
# rate constant for quarantine
kQ 0.0038 # /d
# rate constant for self-isolation
jQ 0.4 # /d
# rate constant for A->R transition
cA 0.26 # /d
# rate constant for H->D or R transition
cH 0.17 # /d
# fraction of hospitalized patients who recover
fR 0.79 # dimensionless
# effectiveness of vaccination agaist ancestral strain
f0 0.9
# effectiveness of vaccination agaist Alpha variant
f1 0.81
# effectiveness of vaccination agaist Delta variant
f2 0.69
# rate constant for progression through stages of the vaccine immunity
Kv 0.3
end parameters
begin molecule types
# time tracker
#  We need to track time to account for changes in social distancing
#  and emergence of new SARS-CoV-2 variants.
counter()
# auxilliary function to track case counts
fDCS()
#---
# S: susceptible population
#  comments:
#   - The entire population (less I0) is initially susceptible.
#  population features:
#   SD: non-pharmaceutical intervention (SD) adopted
#    comments:
#     - Persons in the S, E, A, I, and R compartments may
#       change their behavior to prevent disease transmission.
#    possible feature values:
#     - 0: none
#     - P: social distancing
#     - Q: quarantine or self-isolation
#     - M: mixing
#     - V: vaccination
#     comments:
#      - Persons in the SD~M state are mixing/socializing freely.
#        They do not take precautions to prevent disease transmission.
#      - Persons in the SD~P state take precautions to prevent
#        disease transmission.
#      - Persons in the SD~Q state are separated from susceptible persons.
#        They are incapable of transmitting disease.
#      - Persons in the SD~V state are vaccinated. V~0~1~2~3 is to track
#        the different number of vaccine doses that a person has received.
#---
S(SD~M~P)
#---
# SV: vaccinated and suseptible population
#     strains: suseptible to different virus strains
#     possible feature values:
#            -1: suseptible to ancestral strain
#            -2: suseptible to Alpha strain
#            -3: suseptible to Delta strain
#            -4: not suseptible
SV(strains~1~2~3~4)
#---
# E: exposed population in the incubation period of disease progression
#  population features:
#   stage: incubation period stage
#    comments:
#     - The incubation period is divided into 5 stages.
#     - Exposed persons do not have symptoms
#       throughout the incubation period.
#     - Exposed persons are infectious except in the first stage
#       of the incubation period.
#    possible feature values:
#     - 1: 1st stage of the incubation period
#     - 2: 2nd stage of the incubation period
#     - 3: 3rd stage of the incubation period
#     - 4: 4th stage of the incubation period
#     - 5: 5th stage of the incubation period
#     comments:
#      - Exposed persons in the first stage of the incubation period
#        are ineligible for quarantine (because virus cannot be detected).
#   SD: non-pharmaceutical intervention (SD) adopted
#      possible feature values:
#     - 0: none
#     - P: social distancing
#     - Q: quarantine or self-isolation
#---
E(stage~1~2~3~4~5,SD~M~P~Q,V~0~1)
#---
# A: asymptomatic population
#  comments:
#   - Asymptomatic persons have passed through the incubation period.
#   - Asympotmatic persons are infectious.
#   - Asymptomatic persions will never develop symptoms.
#  population feature values:
#   SD: non-pharmaceutical intervention (SD) adopted
#    possible feature values:
#     - 0: none
#     - P: social distancing
#     - Q: quarantine or self-isolation
#---
A(SD~M~P~Q,V~0~1)
#---
# I: population with mild symptomatic disease
#  comments:
#   - Symptomatic persons are more likely to self-isolate than
#     pre-symptomatic or asymptomatic persons because of symptom awareness.
#  population features:
#    SD: non-pharmaceutical intervention (SD) adopted
#     possible feature values:
#      - 0: none
#      - P: social distancing
#      - Q: quarantine or self-isolation
#---
I(SD~M~P~Q,V~0~1)
# V: vaccinated population
#  comments:
#   - Vaccinated person in the SD~M do not take precautions to prevent
#     disease transmission. This population is still at risk of infection.
#---
V(stage~1~2~3~4~5~6)
#---
# H: population with severe disease
#  comments:
#   - Persons with severe disease are in hospital or isolated at home.
#     Consequently, they are unable to transmit disease.
#---
H(V~0~1)
#---
# R: recovered population
#  population features:
#   SD: non-pharmaceutical intervention (SD) adopted
#    possible feature values:
#     - 0: none
#     - P: social distancing
#     - Q: quarantine or self-isolation
#---
R(V~0~1)
#---
# D: deceased population
D()
end molecule types
begin seed species
counter() 0
S(SD~M) S0-I0
I(SD~M,V~0) I0
fDCS() 0
#
end seed species
begin observables
Molecules S S()
Molecules E E()
Molecules A A()
Molecules I I()
Molecules H H()
Molecules R R()
Molecules D D()
Molecules fDCs_Cum fDCS()
Molecules S_M S(SD~M)
Molecules S_P S(SD~P)
Molecules Einf_M_ E(stage~2,SD~M,V~0) E(stage~3,SD~M,V~0) E(stage~4,SD~M,V~0) E(stage~5,SD~M,V~0)
Molecules Einf_M_1 E(stage~1,SD~M,V~0) E(stage~2,SD~M,V~0) E(stage~3,SD~M,V~0) E(stage~4,SD~M,V~0) E(stage~5,SD~M,V~0)
Molecules A_M A(SD~M,V~0) 
#Molecules I_M_python I(SD~M,V~0) I(SD~M,V~1)
Molecules I_M I(SD~M,V~0) 
Molecules Einf_P_ E(stage~2,SD~P) E(stage~3,SD~P) E(stage~4,SD~P) E(stage~5,SD~P)
Molecules Einf_P_1 E(stage~1,SD~P) E(stage~2,SD~P) E(stage~3,SD~P) E(stage~4,SD~P) E(stage~5,SD~P)
Molecules A_P A(SD~P)
#Molecules A_P_python A(SD~P,V~0) 
Molecules A_ A(SD~M) A(SD~P)
Molecules A_MP A(SD~M,V~0) A(SD~P,V~0)
Molecules I_P I(SD~P,V~0)
#Molecules I_P_python I(SD~P,V~0)
Molecules I_ I()
Molecules A_V A(SD~M,V~1)
Molecules I_V I(SD~M,V~1)
Molecules V V()
Molecules H_ H()
Molecules H_v H(V~0)
Molecules R_ R()
Molecules R_MP R(V~0)
Molecules D_ D()
Molecules E5 E(SD~M,stage~5)
Molecules nE5_MP E(stage~5,SD~M,V~0) E(stage~5,SD~P,V~0) 
Molecules nE_V E(V~1)
Molecules nE4_MP E(stage~4,SD~M) E(stage~4,SD~P)
Molecules nE2_MP E(stage~2,SD~M) E(stage~2,SD~P)
Molecules nE3_MP E(stage~3,SD~M) E(stage~3,SD~P)
Molecules t counter()
end observables
begin functions
# vaccination rate of fully vaccination population
# data is obtained from COVID Act Now
# In the Python code, we use reports of completed vaccinations to set the 
# daily vaccination rate. Here, as a simplification, we use reports of completed
# vaccinations to set the weekly vaccination rate. 
# This difference does not significantly change simulation results.
# region specific
v_rate()=if(t<=350,0,if(t<=358,0.0001151300164394649,if(t<=365,0.0001815703150206857,if(t<=372,0.00021238159793562512,if(t<=379,0.00038367249969829267,if(t<=386,0.0006374374051095428,if(t<=393,0.003386632690150829,if(t<=400,0.0028490636042055,if(t<=407,0.0026510964984681287,if(t<=414,0.0027366919942190997,if(t<=421,0.002445645677001029,if(t<=428,0.002322522678398557,if(t<=435,0.003623682497971814,if(t<=442,0.004655951208369357,if(t<=449,0.0056297465511269285,if(t<=456,0.005713021634678571,if(t<=463,0.0057228669966967,if(t<=470,0.005170339064829085,if(t<=477,0.006247628558940329,if(t<=484,0.005235969111987143,if(t<=491,0.004410046278818358,if(t<=498,0.002593893603596028,if(t<=505,0.003090848034068043,if(t<=512,0.0035200823824075712,if(t<=519,0.0019015499822598856,if(t<=526,0.0017907282223939283,if(t<=533,0.0011454372780878284,if(t<=540,0.0006681528290099714,if(t<=547,0.0015497398570088145,if(t<=554,0.0007070711021144857,if(t<=561,0.0007619162920181001,if(t<=568,0.0007832827937775714,if(t<=575,0.0007811842661870715,if(t<=582,0.0006614805251334714,if(t<=589,0.0,if(t<=596,0.0,if(t<=603,0.0,if(t<=610,0.0,if(t<=617,0.0,if(t<=624,0.0,0))))))))))))))))))))))))))))))))))))))))
# Total number of persons that are eligible for vaccination
PhiV()=if(t>=ts0,R_MP+Einf_P_1+Einf_M_1+S_M+S_P+A_MP,0)
# region specific
v_times_PhiV()=if(t<=350,0,S0*v_rate()/PhiV())
#v_times_PhiV_python()=if(t<=346,0,S0*v_rate()/PhiV())
# Set the values of the rate constants kQ, jQ, and cI to 0 if t<ts0.
# (We want [I] to remain constant at I0 until t=ts0.)
kQfunc()=if(t>=ts0,kQ,0)
jQfunc()=if(t>=ts0,jQ,0)
cIfunc()=if(t>=ts0,cI,0)
# effective population of infectious persons with SD~M state (mixing)
#  The effective population size is set to 0 if t<ts0.
phiM()=if(t>=ts0,rhoE*Einf_M_+rhoA*A_M+rhoE*nE_V+I_M+rhoA*A_V+I_V,0)
# effective population of infectious persons with SD~P state (protected)
phiP()=if(t>=ts0,rhoE*Einf_P_+rhoA*A_P+I_P,0)
# Step function for variant-specific transmissibility multiplier
Ytheta1_()=if(t<=theta0,1,\
      if(t<=theta1,y0,y1))
# Heaviside function 
U()=if(t>=ts0,1,0)
U_theta1()=if(t<theta0,0,1)
U_theta2()=if(t<theta1,0,1)
#cummulative sum to compute social distancing start time
sum2=ts1+ts2
sum3=ts1+ts2+ts3
sum4=ts1+ts2+ts3+ts4
sum5=ts1+ts2+ts3+ts4+ts5
# social-distancing eigenvalues
#  Lambda() is a step function.
#  Currently, Lambda() provides eigenvalues for 5 social-distancing periods.
Lambda()=if(t>=ts1 && t<sum2,lambda0,\
         if(t>=sum2 && t<sum3,lambda1,\
         if(t>=sum3 && t<sum4,lambda2,\
         if(t>=sum4 && t<sum5,lambda3,\
         if(t>=sum5,lambda4,\
         0)))))
# social-distancing setpoint fractions
#  P() is a step function.
#  Currently, P() provides setpoint fractions for 5 social-distancing periods.
P()=if(t>=ts1 && t<sum2,p0,\
    if(t>=sum2 && t<sum3,p1,\
    if(t>=sum3 && t<sum4,p2,\
    if(t>=sum4 && t<sum5,p3,\
    if(t>=sum5,p4,\
    0)))))
end functions
begin reaction rules
# cummulative infected population
0->fDCS() fD*(1-fA)*(kL*nE5_MP+kL/5*nE_V)
# increment time
0->counter() 1
########################################
# EID model in near original BNGL form #
########################################
# disease transmission
S(SD~M)->E(stage~1,SD~M,V~0) (Ytheta1_()*beta/S0)*(phiM()+mb*phiP())
S(SD~P)->E(stage~1,SD~P,V~0) mb*(Ytheta1_()*beta/S0)*(phiM()+mb*phiP())
# social distancing
S(SD~M)<->S(SD~P) U()*Lambda()*P(),U()*Lambda()*(1-P())
# there are five stages between E(SD~M) and E(SD~P)
E(stage~1,SD~M,V~0)<->E(stage~1,SD~P,V~0) U()*Lambda()*P(),U()*Lambda()*(1-P())
E(stage~2,SD~M,V~0)<->E(stage~2,SD~P,V~0) U()*Lambda()*P(),U()*Lambda()*(1-P())
E(stage~3,SD~M,V~0)<->E(stage~3,SD~P,V~0) U()*Lambda()*P(),U()*Lambda()*(1-P())
E(stage~4,SD~M,V~0)<->E(stage~4,SD~P,V~0) U()*Lambda()*P(),U()*Lambda()*(1-P())
E(stage~5,SD~M,V~0)<->E(stage~5,SD~P,V~0) U()*Lambda()*P(),U()*Lambda()*(1-P())
# social distancing
A(SD~M,V~0)<->A(SD~P,V~0) U()*Lambda()*P(),U()*Lambda()*(1-P())
I(SD~M,V~0)<->I(SD~P,V~0) U()*Lambda()*P(),U()*Lambda()*(1-P())
# testing-driven quarantine
E(stage~2,SD~M,V~0)->E(stage~2,SD~Q,V~0) kQ
E(stage~3,SD~M,V~0)->E(stage~3,SD~Q,V~0) kQ
E(stage~4,SD~M,V~0)->E(stage~4,SD~Q,V~0) kQ
E(stage~5,SD~M,V~0)->E(stage~5,SD~Q,V~0) kQ
E(stage~2,SD~P,V~0)->E(stage~2,SD~Q,V~0) kQ
E(stage~3,SD~P,V~0)->E(stage~3,SD~Q,V~0) kQ
E(stage~4,SD~P,V~0)->E(stage~4,SD~Q,V~0) kQ
E(stage~5,SD~P,V~0)->E(stage~5,SD~Q,V~0) kQ
# progression through stages of the incubation period
E(stage~1,SD~M,V~0)->E(stage~2,SD~M,V~0) kL
E(stage~2,SD~M,V~0)->E(stage~3,SD~M,V~0) kL
E(stage~3,SD~M,V~0)->E(stage~4,SD~M,V~0) kL
E(stage~4,SD~M,V~0)->E(stage~5,SD~M,V~0) kL
E(stage~1,SD~P,V~0)->E(stage~2,SD~P,V~0) kL
E(stage~2,SD~P,V~0)->E(stage~3,SD~P,V~0) kL
E(stage~3,SD~P,V~0)->E(stage~4,SD~P,V~0) kL
E(stage~4,SD~P,V~0)->E(stage~5,SD~P,V~0) kL
E(stage~2,SD~Q,V~0)->E(stage~3,SD~Q,V~0) kL
E(stage~3,SD~Q,V~0)->E(stage~4,SD~Q,V~0) kL
E(stage~4,SD~Q,V~0)->E(stage~5,SD~Q,V~0) kL
# entry into the immune clearance phase without generation of symptoms
E(stage~5,SD~M,V~0)->A(SD~M,V~0) kL*fA
E(stage~5,SD~P,V~0)->A(SD~P,V~0) kL*fA
E(stage~5,SD~Q,V~0)->A(SD~Q,V~0) kL*fA
# entry into the immune clearance phase *with* generation of mild symptoms
E(stage~5,SD~M,V~0)->I(SD~M,V~0) kL*(1-fA)
E(stage~5,SD~P,V~0)->I(SD~P,V~0) kL*(1-fA)
E(stage~5,SD~Q,V~0)->I(SD~Q,V~0) kL*(1-fA)
# self-isolation because of symptom awareness
I(SD~M,V~0)->I(SD~Q,V~0) jQfunc()+kQfunc()
I(SD~P,V~0)->I(SD~Q,V~0) jQfunc()+kQfunc()
A(SD~M,V~0)->A(SD~Q,V~0) kQfunc()
A(SD~P,V~0)->A(SD~Q,V~0) kQfunc()
# immune clearance, recovery from asymptomatic infection
A(SD~M,V~0)->R(V~0) cA
A(SD~P,V~0)->R(V~0) cA
A(SD~Q,V~0)->R(V~0) cA
# immune clearance, recovery from mild symptomatic infection
I(SD~M,V~0)->R(V~0) cIfunc()*(1-fH)
I(SD~P,V~0)->R(V~0) cIfunc()*(1-fH)
I(SD~Q,V~0)->R(V~0) cIfunc()*(1-fH)
# progression from mild to severe disease & hospitalization/isolation-at-home
I(V~0)->H(V~0) cIfunc()*fH
# recovery from severe disease
H(V~0)->R(V~0) cH*fR
# progression from severe disease to death
H(V~0)->D() cH*(1-fR)
# vaccination
#  Rules are added to capture the effects of vaccination.
# vaccinate
S(SD~M)->V(stage~1) v_times_PhiV()
S(SD~P)->V(stage~1) v_times_PhiV()
V(stage~1)->V(stage~2) Kv
V(stage~2)->V(stage~3) Kv
V(stage~3)->V(stage~4) Kv
V(stage~4)->V(stage~5) Kv
V(stage~5)->V(stage~6) Kv
#Assume infected and vaccinated population are SD~M.
V(stage~1)->E(stage~1,SD~M,V~1) (Ytheta1_()*beta/S0)*(phiM()+mb*phiP())
V(stage~2)->E(stage~1,SD~M,V~1) (Ytheta1_()*beta/S0)*(phiM()+mb*phiP())
V(stage~3)->E(stage~1,SD~M,V~1) (Ytheta1_()*beta/S0)*(phiM()+mb*phiP())
V(stage~4)->E(stage~1,SD~M,V~1) (Ytheta1_()*beta/S0)*(phiM()+mb*phiP())
V(stage~5)->E(stage~1,SD~M,V~1) (Ytheta1_()*beta/S0)*(phiM()+mb*phiP())
V(stage~6)->E(stage~1,SD~M,V~1) (Ytheta1_()*beta/S0)*(phiM()+mb*phiP())
# a fraction of vaccinated persons remain susceptible to infection
V(stage~6)->SV(strains~1) Kv*(1-f0)
V(stage~6)->SV(strains~2) Kv*(f0-f1)
V(stage~6)->SV(strains~3) Kv*(f1-f2)
V(stage~6)->SV(strains~4) Kv*f2
#For vaccinated persons, as a simplification,
# we assume that the incubation period consists of only a single stage.
SV(strains~1)->E(stage~1,SD~M,V~1) (Ytheta1_()*beta/S0)*(phiM()+mb*phiP())
SV(strains~2)->E(stage~1,SD~M,V~1) (Ytheta1_()*U_theta1()*beta/S0)*(phiM()+mb*phiP())
SV(strains~3)->E(stage~1,SD~M,V~1) (Ytheta1_()*U_theta2()*beta/S0)*(phiM()+mb*phiP())
# entry into the immune clearance phase without generation of symptoms
E(stage~1,SD~M,V~1)->A(SD~M,V~1) (kL/5)*fA
E(stage~1,SD~M,V~1)->I(SD~M,V~1) (kL/5)*(1-fA)
# immune clearance, recovery
A(SD~M,V~1)->R(V~1) cA
I(SD~M,V~1)->R(V~1) cIfunc()*(1-fH/25)
I(SD~M,V~1)->H(V~1) cIfunc()*(fH/25)
H(V~1)->R(V~1) cH*fR
H(V~1)->D() cH*(1-fR)
# recovered and unvaccinated persons are elegible for vaccination 
R(V~0)->R(V~1) v_times_PhiV()
end reaction rules
end model
begin actions
generate_network({overwrite=>1})
simulate({suffix=>"DC",method=>"ode",t_end=>581,n_steps=>581,print_functions=>1,atol=>1e-7,rtol=>1e-7})
end actions

